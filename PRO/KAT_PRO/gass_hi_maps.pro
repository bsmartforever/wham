pro gass_hi_maps, files, dir, click=click, ps=ps, vrange=vrange, zrange=zrange, $
  structure=structure,factor=factor,$
	magellanic=magellanic, bar=bar, lon_loc=lon_loc ,lat_loc=lat_loc, tag=tag,$
    whambeam=whambeam, linear=linear, log=log, levels=levels, thick=thick,$
    c_color=c_color,smooth=smooth,charsize=charsize,charthick=charthick,$
    symsize=symsize,lonlabtop=lonlabtop,radius=radius,$
    left=left,right=right,top=top,bottom=bottom

;+
;
; Purpose: To create quickly create and view GASS Survey HI maps generated by 
;	the parkes_hi.pro. 
;
; files   - Name of the *.sav file containing the GASS Survey NHI map. 
;		    Can contain a string of filenames.
; dir     - Directory containing the *.sav files. Only accepts one directory.
; click   - Calls curspect.pro to allow user to click on various locations on 
;		    	the map and plot the spectrum. The plotted spectrum will span the 
;		  	specified vrange. If keyword ps and click is passed, ps is unset.  
; ps      - Sets the output to a postscript file with the root name of files, 
;		  	with any dash convertied to an underscore.
; vrange  - Velocity range for integrating the HI map. Default is to integrate 
;		 	over all velocities. This range is applied to all maps passed 
;		 	in files. 
; zrange  - Specifies the NHI range plotted in the NHI map. 
; magellanic - Plots the map in Magellanic coordinants instead of Galactic. 
; bar     - Set to display colorbar
; lon_loc - Overplot symbols on the map at the specified longitude. 
; lat_loc - Overplot symbols on the map at the specified latitude.
;				Both lon_loc and lat_loc must both be passed and contain the 
;				same number of elements and both be in the mapped coordinant 
;               system, e.g., if 'magellanic' is set, then pass magellanic 
;               coordinants, else pass Galactic. 
; levels  - contour levels
; factor  - Exponential factor for colorbar, e.g., factor=18 for 10^18 cm^-2
;
;
; Created by Dr. Kat Barger 5/2013
;-

p_str=!p

if (NOT keyword_set(dir)) then dir='./'

num=n_elements(files)

files=strarr(num)+files

;Store original color table
tvlct, red, green, blue, /get
@kat_color

if size(color,/type) eq 7 then begin
   color=cgColor(color,/triple)
   TVLCT,color,254
endif else begin
    TVLCT, 0, 0, 0, 254       ; Black color
    !P.Color = 254
    color=254
endelse

!p.position=[0.1,0.1,0.9,0.9]

if keyword_set(click) AND keyword_set(ps) then begin
   print,' '
   print,'*** Can specify either click or ps, but not both. ***'
   print,'       *** Setting plotting to terminal ***'
   print,' '
   ps=0
endif

if n_elements(thick) ne 0 then beam_thick=thick


for i=0, num-1 do begin

   if (NOT keyword_set(structure)) then begin
	   file=validate_extension(files[i],'sav')
	   restore,dir+file
	   varname=rm_ext(file,'sav',/dash)
      hi_map=(SCOPE_VARFETCH(varname,/enter,level=0))
   endif else hi_map=(SCOPE_VARFETCH(structure,/enter,level=0))

     if (NOT keyword_set(vrange)) then $
     	vrange=[min(hi_map[0].vel),max(hi_map[0].vel)] $
 	 else vrange=[min(vrange),max(vrange)]

     vindex=where((hi_map[0].vel ge vrange[0]) AND (hi_map[0].vel le vrange[1]))

     num_sightlines=n_elements(hi_map)

     hi_int=fltarr(num_sightlines)
     for j=0L,num_sightlines-1 do hi_int[j]=int_tabulated(hi_map[j].vel(vindex),hi_map[j].data(vindex))*1.823e18

     if (NOT keyword_set(zrange)) then begin
     	zrange=[min(hi_int),max(hi_int)]
     	if zrange[0] lt 0 then zrange[0] = 0
     endif else begin
        zmin=zrange[0]
        zmax=zrange[1]
     endelse

	 beamradius=0.26666667/2.
	 if keyword_set(magellanic) then begin
	 	glon_min=min(hi_map.mlon)-beamradius*2.
	 	glon_max=max(hi_map.mlon)+beamradius*2.
	 	glat_min=min(hi_map.mlat)-beamradius*2.
	 	glat_max=max(hi_map.mlat)+beamradius*2.
	 endif else begin
	 	glon_min=min(hi_map.glon)-beamradius*2.
	 	glon_max=max(hi_map.glon)+beamradius*2.
	 	glat_min=min(hi_map.glat)-beamradius*2.
	 	glat_max=max(hi_map.glat)+beamradius*2.
	 endelse


	 if keyword_set(ps) then begin

	 	 ps_name=rm_ext(file,'sav')
	 	 ps_name=validate_extension(ps_name,'eps')

	     name=dir+ps_name

    	 entry_device=!d.name
    	 set_plot,'PS'
    	 !p.font=0
    	 device,filename=name,bits_per_pixel=16,color=1
    	 device,/landscape,encapsulated=1, /helvetica
    	 device,xsize=10,xoffset=(11.0-10)*0.5,/inches

		!p.symsize=1.75
		!p.charthick=3.5
    	if (NOT keyword_set(charsize)) then charsize=1.5
		!p.thick=9
		!p.position=[0.2,0.05,0.8,1]

	 endif else begin

	 	set_plot,'x'

        if (NOT keyword_set(charsize)) then charsize=1.1 

	 endelse    

        if (NOT keyword_set(linear)) AND (NOT keyword_set(log)) then begin
            linear=1
            log=0
        endif 
        if (NOT keyword_set(smooth)) then smooth=0

	 	whammap,hi_map,0,0,hi_int,/useimage,magellanic=magellanic,$
    	smgrid = 0.5,scale = 1.25,cbottom=1,beamradius=beamradius,$ 
    	ymargin=[10.5,5.5], xmargin=[8,8], missing = missing,$
    	limits=[glon_min,glon_max,glat_min,glat_max],zmin = zmin, zmax = zmax, $
    	smooth = smooth,linear=linear,log=log,$
      /nozero,/noframe,/xytitles,$
      charsize=charsize*0.75,charthick=charthick,$
      lonlabtop=lonlabtop

        if keyword_set(levels) then begin

            stupid = hi_int
            if keyword_set(magellanic) then begin
                stupid_glon = -hi_map.mlon
                stupid_glat = hi_map.mlat
            endif else begin
                stupid_glon = -hi_map.glon
                stupid_glat = hi_map.glat
            endelse

            for i=0, n_elements(levels)-1 do begin
                if (NOT keyword_set(c_color)) then c_color=cgColor('blk6')
                cgcontour, stupid, stupid_glon, stupid_glat, levels = levels[i], $
                  /irreg, c_labels = 0,/overplot,$
                  c_color=c_color,c_thick=fix(thick*.5),min_value=0,fill=0
            endfor
        endif

    	if keyword_set(lon_loc) AND keyword_set(lat_loc) AND (NOT keyword_set(WHAMBEAM)) $
    		AND (n_elements(lon_loc) eq n_elements(lat_loc)) then $
    		for q=0, n_elements(lon_loc)-1 do $
    			plots,-lon_loc[q],lat_loc[q],psym=4,thick=thick,symsize=symsize,color=color,_extra=extra $
        else if keyword_set(lon_loc) AND keyword_set(lat_loc) AND keyword_set(WHAMBEAM) then begin
            radius=sqrt(1./!pi)
            if (NOT keyword_set(magellanic)) then begin
                num_actual = n_elements(lon_loc)
                if n_elements(lon_loc) le 1.0 then num = 2 else num = n_elements(lon_loc)
                beams=replicate({glon:lon_loc[0],glat:lat_loc[0]},n_elements(lon_loc),num)
                beams[0:num_actual-1].glon=lon_loc
                beams[0:num_actual-1].glat=lat_loc
            endif else begin
                num_actual = n_elements(lon_loc)
                if n_elements(lon_loc) le 1.0 then num = 2 else num = n_elements(lon_loc)
                beams=replicate({glon:lon_loc[0],glat:lat_loc[0],mlon:lon_loc[0],mlat:lat_loc[0]},num)
                mag2gal,lon_loc,lat_loc,glon_loc,glat_loc
                beams[0:num_actual-1].mlon=lon_loc
                beams[0:num_actual-1].mlat=lat_loc
                beams[0:num_actual-1].glon=glon_loc
                beams[0:num_actual-1].glat=glat_loc
            endelse
            whammap,beams,/beamsonly,beam_thick=beam_thick,radius=radius,$
            /nogrid,/noborder,/noerase,_extra=extra,$
            limits=[glon_min,glon_max,glat_min,glat_max],/nolabels,magellanic=magellanic
        endif

      if keyword_set(lon_loc) AND keyword_set(lat_loc) AND keyword_set(tag) then begin
        if keyword_set(WHAMBEAM) then delta=radius*1.75 else delta=(glon_max-glon_min)*.05

        for i=0, n_elements(lon_loc)-1 do begin

            xpos=-lon_loc[i] & ypos=lat_loc[i]
            if (NOT keyword_set(top)) AND (NOT keyword_set(bottom)) AND (NOT keyword_set(left)) AND (NOT keyword_set(right)) $
                then right=1
            if (keyword_set(top)) then ypos=ypos+delta else if (keyword_set(bottom)) then ypos=ypos-delta
            if (keyword_set(right)) then xpos=xpos+delta else if (keyword_set(left)) then xpos=xpos-delta

            xyouts,xpos,ypos,tag[i],align=0.5,charsize=2,color=fsc_color('black')
        endfor
      endif

    	if keyword_set(bar) then begin

    		!p.position=[0.1,0.1,0.9,0.9]

    		 tmp=strsplit(zmax,'e+',/extract)
    		 if (n_elements(tmp) eq 2) AND (NOT keyword_set(log)) then begin
    		    if (NOT keyword_set(factor)) then begin
                    exponent=float('1.0e'+tmp[1]) 
    		        title='HI Column Density (10!U'+tmp[1]+'!N cm!U-2!N)'
                endif else begin
                    title='HI Column Density (10!U'+strcompress(string(factor),/re)+'!N cm!U-2!N)'
                    exponent=10.^factor
                endelse
    		 endif else begin
    		 	title = 'HI Column Density (cm!U-2!N)'  
    		 	exponent=1
    		 endelse
             @kat_color

       if keyword_set(ps) then begin  
        !x.thick=5
        !y.thick=5
       endif     

			 colorbar, ncolors = !d.table_size-1, range = [zmin, zmax]/exponent, $
			   position = [!x.window[1]*1.07, !y.window[0], !x.window[1]*1.125, !y.window[1]], $
			   VERTICAL=1,right=1,charsize=charsize,charthick=charthick,$
         _extra=extra,color=color,ylog=log
             if keyword_set(log) then space_factor=1.02 else space_factor=0.95 
             xyouts,(!x.window[1]*1.25)*space_factor,((!y.window[1]-!y.window[0])/2.+!y.window[0]),$
                title,orientation=90,align=0.5,/normal,charthick=charthick,charsize=charsize,$
                color=color

    	endif

	 if keyword_set(ps) then begin

    	!p.multi=0
    	!p.thick=1
    	!x.thick=1
    	!y.thick=1
	
    	device,/close_file
    	set_plot,entry_device
    	Close, /All
	
    	set_plot,'x'

	 endif   


endfor

if keyword_set(click) then $
	curspect,hi_map,map_scale=[1.823e18],magellanic=magellanic,$
		color=color,xrange=vrange,xstyle=1,charsize=charsize,$
		charthick=charthick,_extra=extra,radius=radius

;restore original color table
tvlct, red, green, blue
!p=p_str

end 